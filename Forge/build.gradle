plugins {
    alias libs.plugins.forgegradle
    alias libs.plugins.librarian
    alias libs.plugins.mixin
    alias libs.plugins.cursegradle
    alias libs.plugins.minotaur
}

base.archivesName = "${mod_id}-forge-${libs.versions.minecraft.get()}"

minecraft {
    mappings channel: 'parchment', version: "${libs.versions.parchment.minecraft.get()}-${libs.versions.parchment.mappings.get()}-${libs.versions.minecraft.get()}"

    runs {
        client {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Client'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                modClientRun {
                    source sourceSets.test
                    source sourceSets.main
                    source project(":Common").sourceSets.test
                    source project(":Common").sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run-server')
            ideaModule "${rootProject.name}.${project.name}.main"
            taskName 'Server'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                modServerRun {
                    source sourceSets.test
                    source sourceSets.main
                    source project(":Common").sourceSets.test
                    source project(":Common").sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')
            ideaModule "${rootProject.name}.${project.name}.main"
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            taskName 'Data'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            mods {
                modDataRun {
                    source sourceSets.main
                    source project(":Common").sourceSets.main
                }
            }
        }
    }
}


sourceSets.main.resources.srcDir 'src/generated/resources'

repositories {
}

jarJar.enable()

dependencies {
    minecraft libs.forge

    implementation(annotationProcessor(libs.mixinextras.common.get()))

    implementation(libs.mixinextras.forge.get())

    jarJar(libs.mixinextras.forge.get()) {
        jarJar.ranged(it, "[${libs.versions.mixinextras.get()},)")
    }

    annotationProcessor(variantOf(libs.mixin) { classifier 'processor' })

    compileOnly(variantOf(libs.gml) { classifier 'all' })
    runtimeOnly(fg.deobf(variantOf(libs.gml) { classifier 'all' }.get())) {
        exclude module: 'transform'
    }

    compileOnly fg.deobf(libs.cgl.forge.get())

    compileOnly project(path: ":Common", configuration: "namedElements")
    testCompileOnly project(path: ':Common', configuration: 'testOut')
}

mixin {
    add sourceSets.main, 'jsonwrangler.refmap.json'
    //config 'mixin.jsonwrangler.forge.json'
    add project(":Common").sourceSets.main, 'jsonwrangler.refmap.json'
    config 'mixin.jsonwrangler.json'
}

tasks.withType(GroovyCompile) {
    source project(":Common").sourceSets.main.allGroovy
}

tasks.withType(JavaCompile) {
    source project(":Common").sourceSets.main.allJava
}

processResources {
    from project(':Common').sourceSets.main.resources
    exclude 'mods.groovy'
}

jar {
    archiveClassifier = 'partial'
    finalizedBy('reobfJar')
}

tasks.build.dependsOn tasks.jarJar

reobf {
    jarJar {}
}

tasks.jarJar {
    archiveClassifier = ''
    finalizedBy tasks.reobfJarJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifacts = [tasks.jarJar, sourcesJar]
        }
    }
}

if (System.getenv('CURSEFORGE_KEY')) {
    curseforge {
        apiKey = System.getenv('CURSEFORGE_KEY')
        project {
            id = '843930'
            releaseType = "${project.release_type}"
            addGameVersion libs.versions.minecraft.get()
            addGameVersion 'Forge'
            mainArtifact(tasks.jarJar) {
                displayName = "JsonWrangler [Forge] v$project.version"
            }
            relations {
                requiredDependency 'gml'
            }
        }
        options {
            forgeGradleIntegration = true
        }
    }
}

if (System.getenv('MODRINTH_KEY')) {
    modrinth {
        token = System.getenv('MODRINTH_KEY')
        projectId = 'XodEFO40'
        versionNumber = "$project.version"
        versionType = "${project.release_type}"
        detectLoaders = false
        uploadFile = tasks.jarJar
        gameVersions = [libs.versions.minecraft.get()]
        loaders = ['forge']
        dependencies {
            required.project 'gml'
        }
    }
}
